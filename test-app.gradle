import org.gradle.initialization.DefaultSettings

private static void apply(Settings settings) {
    def projectDir = settings.findNodeModulesPath(settings.rootDir, "react-native-test-app")

    settings.include(":app")
    settings.project(":app")
            .projectDir = new File("${projectDir}/android/app")
}

def scriptDir = buildscript.sourceFile.getParent()

apply from: "$scriptDir/android/test-app-util.gradle"
apply from: "$scriptDir/android/test-app-native-modules.gradle"

ext.applyTestAppSettings = { DefaultSettings defaultSettings ->
    apply(defaultSettings)
    applyNativeModulesSettingsGradle(defaultSettings)
}

ext.applyTestAppModule = { Project project, String packageName ->
    applyNativeModulesAppBuildGradle(project, packageName)

    def generatedAssetsDir = file("${buildDir}/generated/rncli/src/main/assets/")
    generatedAssetsDir.mkdirs()

    def generatedResDir = file("${buildDir}/generated/rncli/src/main/res/")
    generatedResDir.mkdirs()

    task copyBundle(type: Copy) {
        def bundlePath = project.getProperties()
                .get("testApp.bundle")

        from "${rootDir}/${bundlePath}"
        into generatedAssetsDir
    }

    task copyResources(type: Copy) {
        def resourcesPath = project.getProperties()
                .get("testApp.resources")

        from "${rootDir}/${resourcesPath}"
        into generatedResDir
    }

    preBuild.dependsOn(copyBundle)
    preBuild.dependsOn(copyResources)

    android {
        sourceSets {
            main {
                assets.srcDirs += generatedAssetsDir
                res.srcDirs += generatedResDir
            }
        }
    }
}
