import groovy.json.JsonSlurper

import java.nio.file.Paths

ext.buildReactNativeFromSource = { baseDir ->
    def reactNativePath = findNodeModulesPath(baseDir, 'react-native')
    return !file("${reactNativePath}/android").exists()
}

ext.findFile = { fileName ->
    def currentDirPath = rootDir == null ? null : rootDir.toString()

    while (currentDirPath != null) {
        def currentDir = file(currentDirPath);
        def requestedFile = Paths.get(currentDirPath, fileName).toFile()

        if (requestedFile.exists()) {
            return requestedFile
        }

        currentDirPath = currentDir.getParent()
    }

    return null
}

/**
 * Finds the path of the installed npm package with the given name using Node's
 * module resolution algorithm, which searches "node_modules" directories up to
 * the file system root. This handles various cases, including:
 *
 *   - Working in the open-source RN repo:
 *       Gradle: /path/to/react-native/ReactAndroid
 *       Node module: /path/to/react-native/node_modules/[package]
 *
 *   - Installing RN as a dependency of an app and searching for hoisted
 *     dependencies:
 *       Gradle: /path/to/app/node_modules/react-native/ReactAndroid
 *       Node module: /path/to/app/node_modules/[package]
 *
 *   - Working in a larger repo (e.g., Facebook) that contains RN:
 *       Gradle: /path/to/repo/path/to/react-native/ReactAndroid
 *       Node module: /path/to/repo/node_modules/[package]
 *
 * The search begins at the given base directory (a File object). The returned
 * path is a string.
 */
ext.findNodeModulesPath = { baseDir, packageName ->
    def basePath = baseDir.toPath().normalize()

    // Node's module resolution algorithm searches up to the root directory,
    // after which the base path will be null
    while (basePath) {
        def candidatePath = Paths.get(basePath.toString(), 'node_modules', packageName)
        if (candidatePath.toFile().exists()) {
            return candidatePath.toString()
        }

        basePath = basePath.getParent()
    }

    return null
}

ext.getAppName = { baseDir ->
    def manifestFile = findFile('app.json')
    if (manifestFile != null) {
        def manifest = new JsonSlurper().parseText(manifestFile.text)

        def displayName = manifest['displayName']
        if (displayName instanceof String) {
            return displayName
        }

        def name = manifest['name']
        if (name instanceof String) {
            return name
        }
    }

    return "ReactTestApp"
}

ext.getApplicationId = { baseDir ->
    def manifestFile = findFile('app.json')
    if (manifestFile != null) {
        def manifest = new JsonSlurper().parseText(manifestFile.text)
        def config = manifest['android']
        if (config instanceof Object && config.containsKey('package')) {
            return config['package']
        }
    }

    if (project.hasProperty('TEST_APP_IDENTIFIER')) {
        def applicationId = project.getProperty('TEST_APP_IDENTIFIER')
        logger.warn('WARNING: Setting TEST_APP_IDENTIFIER in `gradle.properties` is deprecated')
        logger.warn('WARNING: Please set the application identifier in `app.json`, e.g.')
        logger.warn('WARNING:')
        logger.warn('WARNING:     {')
        logger.warn('WARNING:       "name": "Example",')
        logger.warn('WARNING:       "displayName": "Example",')
        logger.warn('WARNING:       "components": [],')
        logger.warn('WARNING:       "resources": {},')
        logger.warn('WARNING:       "android": {')
        logger.warn("WARNING:         \"package\": \"${applicationId}\"")
        logger.warn('WARNING:       }')
        logger.warn('WARNING:     }')
        return applicationId
    }

    return 'com.microsoft.reacttestapp'
}

ext.getFlipperRecommendedVersion = { baseDir ->
    def reactNativePath = findNodeModulesPath(baseDir, 'react-native')
    def props = new Properties()
    file("${reactNativePath}/template/android/gradle.properties").withInputStream {
        props.load(it)
    }

    return props.getProperty('FLIPPER_VERSION')
}

ext.getFlipperVersion = { baseDir ->
    def recommendedFlipperVersion = getFlipperRecommendedVersion(baseDir)
    if (recommendedFlipperVersion == null) {
        // Current React Native version doesn't support Flipper
        return null
    }

    // Prefer user specified Flipper version
    if (project.hasProperty('FLIPPER_VERSION')) {
        def flipperVersion = project.getProperty('FLIPPER_VERSION')
        return flipperVersion == 'false' ? null : flipperVersion
    }

    // Use the recommended Flipper version
    return recommendedFlipperVersion
}

ext.getReactNativeVersionNumber = { baseDir ->
    def reactNativePath = findNodeModulesPath(baseDir, "react-native")
    def packageJson = file("${reactNativePath}/package.json")
    def manifest = new JsonSlurper().parseText(packageJson.text)
    def (major, minor, patch) = manifest["version"].findAll(/\d+/)
    return (major as int) * 10000 + (minor as int) * 100 + (patch as int)
}

ext.getSigningConfigs = { baseDir ->
    def safeSetMap = { varName, map, prop, defaultVal ->
        map[varName] = prop.containsKey(varName) ? prop.get(varName) : defaultVal
    }

    def definedConfigs = new LinkedHashMap<String, Object>()
    def manifestFile = findFile('app.json')
    if (manifestFile != null) {
        def manifest = new JsonSlurper().parseText(manifestFile.text)

        if (!manifest['android']) {
            return definedConfigs
        }

        def signingConfigs = manifest['android']['signingConfigs']
        if (signingConfigs) {
            signingConfigs.each { config ->
                def configName = config.key
                def props = config.value
                def pathStoreFile = props.containsKey('storeFile') ?
                    Paths.get(rootDir.toString(), props.get('storeFile')).normalize().toAbsolutePath() : null
                if (pathStoreFile == null || !file(pathStoreFile).exists() || !file(pathStoreFile).isFile()) {
                    throw new FileNotFoundException("Signing storeFile for flavor ${configName} is missing: " + pathStoreFile)
                }

                def signConfig = new LinkedHashMap<String, Object>()
                safeSetMap('keyAlias', signConfig, props, 'androiddebugkey')
                safeSetMap('keyPassword', signConfig, props, 'android')
                safeSetMap('storePassword', signConfig, props, 'android')
                signConfig['storeFile'] = pathStoreFile.toFile()
                definedConfigs[configName] = signConfig
            }
        }
    }

    return definedConfigs
}