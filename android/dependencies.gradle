/**
 * Find the latest version of KSP that supports the specified Kotlin version.
 */
static String getKspVersion(String kotlinVersion) {
    return [
        // Run `scripts/update-ksp-versions.mjs` to update this list
        // update-ksp-versions start
        "1.8.10-1.0.9",
        "1.8.0-1.0.9",
        "1.7.22-1.0.8",
        "1.7.21-1.0.8",
        "1.7.20-1.0.8",
        "1.7.10-1.0.6",
        "1.7.0-1.0.6",
        "1.6.21-1.0.6",
        "1.6.20-1.0.5",
        "1.6.10-1.0.4",
        "1.6.0-1.0.2",
        "1.5.31-1.0.1",
        "1.5.30-1.0.0",
        // update-ksp-versions end
    ].find { it.startsWith(kotlinVersion) }
}

ext {
    apply(from: "${buildscript.sourceFile.getParent()}/test-app-util.gradle")

    compileSdkVersion = 33
    minSdkVersion = 23
    targetSdkVersion = 29

    reactNativeVersion = getPackageVersionNumber("react-native", rootDir)
    autodetectReactNativeVersion = reactNativeVersion == 0 || reactNativeVersion >= 7100
    enableNewArchitecture = isNewArchitectureEnabled(project)
    usePrefabs = reactNativeVersion == 0 || reactNativeVersion >= 7100

    def useAndroidPlugin_7_2 = reactNativeVersion > 0 && reactNativeVersion < 7100

    // We need only set `ndkVersion` when building react-native from source.
    if (rootProject.hasProperty("ANDROID_NDK_VERSION")) {
        ndkVersion = rootProject.properties["ANDROID_NDK_VERSION"]
    } else if (System.properties["os.arch"] == "aarch64" && useAndroidPlugin_7_2) {
        // NDK r23c has been patched to support Apple M1 and is default in AGP
        // 7.3.0. Prior to 0.71, we still need to set `ndkVersion` because we'll
        // be using AGP 7.2.2 (see `androidPluginVersion` below).
        // Note that even though newer 23.x versions exist, we'll stick to AGP's
        // default. See also
        // https://developer.android.com/studio/releases/gradle-plugin#compatibility-7-3-0
        ndkVersion = "23.1.7779620"
    }

    androidPluginVersion = useAndroidPlugin_7_2
        ? "7.2.2"
        : "7.3.1"
    kotlinVersion = rootProject.hasProperty("KOTLIN_VERSION")
        ? rootProject.properties["KOTLIN_VERSION"]
        : "1.7.10"
    kspVersion = getKspVersion(kotlinVersion)

    /**
     * Dependabot requires a `dependencies.gradle` to evaluate Java
     * dependencies. It is also very particular about the formatting and cannot
     * evaluate string literals.
     *
     * See https://github.com/dependabot/feedback/issues/345.
     */
    libraries = [
        androidAppCompat            : "androidx.appcompat:appcompat:1.4.2",
        androidCamera               : "androidx.camera:camera-camera2:1.2.0-beta02",
        androidCameraMlKitVision    : "androidx.camera:camera-mlkit-vision:1.2.0-beta02",
        androidCoreKotlinExtensions : "androidx.core:core-ktx:1.9.0",
        androidEspressoCore         : "androidx.test.espresso:espresso-core:3.5.1",
        androidJUnit                : "androidx.test.ext:junit:1.1.5",
        androidJUnitKotlinExtensions: "androidx.test.ext:junit-ktx:1.1.5",
        androidRecyclerView         : "androidx.recyclerview:recyclerview:1.2.1",
        androidSwipeRefreshLayout   : "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0",
        junit                       : "junit:junit:4.13.2",
        kotlinReflect               : "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}",
        kotlinStdlibJdk7            : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}",
        kotlinStdlibJdk8            : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}",
        materialComponents          : "com.google.android.material:material:1.7.0",
        mlKitBarcodeScanning        : "com.google.mlkit:barcode-scanning:17.1.0",
        mockitoInline               : "org.mockito:mockito-inline:4.11.0",
        moshiKotlin                 : "com.squareup.moshi:moshi-kotlin:1.14.0",
        moshiKotlinCodegen          : "com.squareup.moshi:moshi-kotlin-codegen:1.14.0",
    ]

    getReactNativeDependencies = {
        def dependencies = ["com.android.tools.build:gradle:${androidPluginVersion}"]

        if (autodetectReactNativeVersion || enableNewArchitecture) {
            dependencies << "com.facebook.react:react-native-gradle-plugin"
            dependencies << "de.undercouch:gradle-download-task:5.4.0"
        }

        return dependencies
    }
}
