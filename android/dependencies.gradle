static String versionOf(String spec) {
    return spec.split(":").last()
}

ext {
    apply(from: "${buildscript.sourceFile.getParent()}/test-app-util.gradle")

    compileSdkVersion = 31
    minSdkVersion = 23
    targetSdkVersion = 29

    // We need only set `ndkVersion` when building react-native from source.
    if (rootProject.hasProperty("ANDROID_NDK_VERSION")) {
        ndkVersion = rootProject.properties["ANDROID_NDK_VERSION"]
    } else if (System.properties["os.arch"] == "aarch64") {
        // Android NDK added support for Apple M1 in r24:
        // https://github.com/android/ndk/wiki/Changelog-r24
        ndkVersion = "24.0.8215888"
    }

    androidPluginVersion = "7.2.2"
    kotlinVersion = rootProject.hasProperty("KOTLIN_VERSION")
        ? rootProject.properties["KOTLIN_VERSION"]
        : "1.7.10"

    /**
     * Dependabot requires a `dependencies.gradle` to evaluate Java
     * dependencies. It is also very particular about the formatting and cannot
     * evaluate string literals.
     *
     * See https://github.com/dependabot/feedback/issues/345.
     */
    libraries = [
        androidAppCompat            : "androidx.appcompat:appcompat:1.4.2",
        androidCoreKotlinExtensions : "androidx.core:core-ktx:1.8.0",
        androidEspressoCore         : "androidx.test.espresso:espresso-core:3.4.0",
        androidJUnit                : "androidx.test.ext:junit:1.1.3",
        androidJUnitKotlinExtensions: "androidx.test.ext:junit-ktx:1.1.3",
        androidRecyclerView         : "androidx.recyclerview:recyclerview:1.2.1",
        androidSwipeRefreshLayout   : "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0",
        junit                       : "junit:junit:4.13.2",
        kotlinReflect               : "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}",
        kotlinStdlibJdk7            : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}",
        kotlinStdlibJdk8            : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}",
        materialComponents          : "com.google.android.material:material:1.6.1",
        mockitoInline               : "org.mockito:mockito-inline:4.7.0",
        moshiKotlin                 : "com.squareup.moshi:moshi-kotlin:1.14.0",
        moshiKotlinCodegen          : "com.squareup.moshi:moshi-kotlin-codegen:1.14.0",
    ]
}
