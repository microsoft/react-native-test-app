static String versionOf(String spec) {
    return spec.split(":").last()
}

ext {
    apply(from: "${buildscript.sourceFile.getParent()}/test-app-util.gradle")

    compileSdkVersion = 33
    minSdkVersion = 23
    targetSdkVersion = 29

    def reactNativeVersion = getPackageVersionNumber("react-native", rootDir)
    def useAndroidPlugin_7_2 = reactNativeVersion > 0 && reactNativeVersion < 7100

    // We need only set `ndkVersion` when building react-native from source.
    if (rootProject.hasProperty("ANDROID_NDK_VERSION")) {
        ndkVersion = rootProject.properties["ANDROID_NDK_VERSION"]
    } else if (System.properties["os.arch"] == "aarch64" && useAndroidPlugin_7_2) {
        // NDK r23c has been patched to support Apple M1 and is default in AGP
        // 7.3.0. Prior to 0.71, we still need to set `ndkVersion` because we'll
        // be using AGP 7.2.2 (see `androidPluginVersion` below).
        // Note that even though newer 23.x versions exist, we'll stick to AGP's
        // default. See also
        // https://developer.android.com/studio/releases/gradle-plugin#compatibility-7-3-0
        ndkVersion = "23.1.7779620"
    }

    androidPluginVersion = useAndroidPlugin_7_2
        ? "7.2.2"
        : "7.3.0"
    kotlinVersion = rootProject.hasProperty("KOTLIN_VERSION")
        ? rootProject.properties["KOTLIN_VERSION"]
        : "1.7.10"

    /**
     * Dependabot requires a `dependencies.gradle` to evaluate Java
     * dependencies. It is also very particular about the formatting and cannot
     * evaluate string literals.
     *
     * See https://github.com/dependabot/feedback/issues/345.
     */
    libraries = [
        androidAppCompat            : "androidx.appcompat:appcompat:1.4.2",
        androidCamera               : "androidx.camera:camera-camera2:1.2.0-beta02",
        androidCameraMlKitVision    : "androidx.camera:camera-mlkit-vision:1.2.0-beta02",
        androidCoreKotlinExtensions : "androidx.core:core-ktx:1.9.0",
        androidEspressoCore         : "androidx.test.espresso:espresso-core:3.4.0",
        androidJUnit                : "androidx.test.ext:junit:1.1.3",
        androidJUnitKotlinExtensions: "androidx.test.ext:junit-ktx:1.1.3",
        androidRecyclerView         : "androidx.recyclerview:recyclerview:1.2.1",
        androidSwipeRefreshLayout   : "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0",
        junit                       : "junit:junit:4.13.2",
        kotlinReflect               : "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}",
        kotlinStdlibJdk7            : "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${kotlinVersion}",
        kotlinStdlibJdk8            : "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}",
        materialComponents          : "com.google.android.material:material:1.7.0",
        mlKitBarcodeScanning        : "com.google.mlkit:barcode-scanning:17.0.2",
        mockitoInline               : "org.mockito:mockito-inline:4.8.1",
        moshiKotlin                 : "com.squareup.moshi:moshi-kotlin:1.14.0",
        moshiKotlinCodegen          : "com.squareup.moshi:moshi-kotlin-codegen:1.14.0",
    ]
}
