buildscript {
    ext.kotlinVersion = "1.3.72"

    def buildscriptDir = buildscript.sourceFile.getParent()
    apply from: "$buildscriptDir/../test-app-util.gradle"

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.android.tools.build:gradle:3.6.2"
    }
}

repositories {
    maven {
        url("${findNodeModulesPath(rootDir, "react-native")}/android")
    }

    google()
    jcenter()
}

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "kotlin-kapt"

def testAppDir = file("$projectDir/../../")

apply from: file("${testAppDir}/test-app.gradle")
applyTestAppModule(project, "com.sample")

project.ext.react = [
    enableFlipper: getFlipperVersion(rootDir),
    enableHermes: true,
]

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    // TODO: Remove this block when minSdkVersion >= 24. See
    // https://stackoverflow.com/q/53402639 for details.
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.sample"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        pickFirst "lib/armeabi-v7a/libc++_shared.so"
        pickFirst "lib/arm64-v8a/libc++_shared.so"
        pickFirst "lib/x86_64/libc++_shared.so"
        pickFirst "lib/x86/libc++_shared.so"
    }

    sourceSets {
        if (project.ext.react.enableFlipper) {
            debug.java.srcDirs += 'src/flipper/java'
        }
    }
}

def hermesEnginePath = findNodeModulesPath(projectDir, "hermes-engine") ?: findNodeModulesPath(projectDir, "hermesvm")
def hermesPath = "$hermesEnginePath/android"

dependencies {
    implementation "com.google.dagger:dagger:2.27"
    implementation "com.google.dagger:dagger-android:2.27"
    implementation "com.google.dagger:dagger-android-support:2.27"

    kapt("com.google.dagger:dagger-compiler:2.27")
    kapt("com.google.dagger:dagger-android-processor:2.27")

    releaseImplementation files("$hermesPath/hermes-release.aar")
    debugImplementation files("$hermesPath/hermes-debug.aar")

    if (buildReactNativeFromSource(rootDir)) {
        implementation project(':ReactAndroid')
    } else {
        implementation "com.facebook.react:react-native:+"
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.1.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"
    implementation "com.google.android.material:material:1.1.0"

    implementation("com.squareup.moshi:moshi-kotlin:1.9.2")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.9.2")

    testImplementation "junit:junit:4.13"
    androidTestImplementation "androidx.test.ext:junit:1.1.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.2.0"

    if (project.ext.react.enableFlipper) {
        def flipperVersion = project.ext.react.enableFlipper
        debugImplementation("com.facebook.flipper:flipper:${flipperVersion}") {
            exclude group:'com.facebook.fbjni'
        }
        debugImplementation("com.facebook.flipper:flipper-fresco-plugin:${flipperVersion}") {
            exclude group:'com.facebook.flipper'
        }
        debugImplementation("com.facebook.flipper:flipper-network-plugin:${flipperVersion}") {
            exclude group:'com.facebook.flipper'
        }
    }
}
