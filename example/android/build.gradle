buildscript {
    def androidTestAppDir = "../node_modules/react-native-test-app/android"
    apply(from: "${androidTestAppDir}/dependencies.gradle")
    apply(from: "${androidTestAppDir}/test-app-util.gradle")

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:${androidPluginVersion}")

        def isNightly = getPackageVersionNumber("react-native", rootDir) == 0
        if (isNightly || isNewArchitectureEnabled(project)) {
            classpath("com.facebook.react:react-native-gradle-plugin")
            classpath("de.undercouch:gradle-download-task:5.3.0")
        }
    }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("${rootDir}/../node_modules/react-native/android")
        }

        def reactNativeVersion = getPackageVersionNumber("react-native", rootDir)
        if (reactNativeVersion == 0 || reactNativeVersion >= 7100) {
            mavenCentral()
        } else {
            mavenCentral {
                content {
                    excludeGroup "com.facebook.react"
                }
            }
        }

        google()
    }
}
